OPTION SPECIALCHAR
OPTION DEFAULTFONT="ms gothic"

const path = "..\UBrowserControl.uws"
const version = "0.0.1"

dim chakra
dim engine = slctbox(SLCT_STR, 0, replace(msg, "%version%", version), "Chakra", "Json2")
select engine
    case "Chakra"
        chakra = TRUE
    case "Json2"
        chakra = FALSE
    default
        exit
selend
dim reason
if ! CheckFiles(chakra, reason) then
    msgbox("必要なファイルが不足しているためビルドできません <#CR>(" + reason + ")")
    exit
endif

deletefile(path)
out = fopen(path, F_READ or F_WRITE8)
fput(out, UBrowserControl(version, engine))

for file in split(files, "<#CR>")
    fid = fopen(file, F_READ)
    script = fget(fid, F_ALLTEXT)
    fclose(fid)
    fput(out, script)
next

// use either Chakra or json2
if chakra then
    file = ".\Modules\Chakra.uws"
else
    file = ".\Modules\Json.uws"
endif
fid = fopen(file, F_READ)
script = fget(fid, F_ALLTEXT)
fclose(fid)
fput(out, script)

fclose(out)

function UBrowserControl(version, engine)
    result = replace(UBrowserControl, "%version%", version)
    result = replace(result, "%engine%", engine)
fend


textblock UBrowserControl
module UBrowserControl
    const Version = "%version%"
    const Engine = "%engine%"
endmodule

endtextblock

textblock files
.\Modules\BrowserControl.uws
.\Modules\Settings.uws
.\Modules\WebSocket.uws
.\Modules\WebRequest.uws
.\Modules\WinHttp.uws
.\Modules\Util.uws
.\Modules\Params.uws
endtextblock

textblock msg
***************************
* UBrowserControl Builder *
***************************

UBrowserControl
Version: %version%

Choose script engine
endtextblock

function CheckFiles(is_chakra, var reason)
    result = TRUE
    result = fopen(".\Modules\", F_EXISTS)
    if ! result then
        reason = "Modulesフォルダがありません"
        exit
    endif
    for file in split(files, "<#CR>")
        result = fopen(file, F_EXISTS)
        if ! result then
            reason = replace(replace(file, ".\", ""), "\", "フォルダに") + "がありません"
            exit
        endif
    next
    if is_chakra then
        result = fopen(".\Modules\Chakra.uws", F_EXISTS)
        if ! result then
            reason = "ModulesフォルダにがChakra.uwsありません"
            exit
        endif
    else
        result = fopen(".\Modules\Json.uws", F_EXISTS)
        if ! result then
            reason = "ModulesフォルダにがJson.uwsありません"
            exit
        endif
    endif
fend

